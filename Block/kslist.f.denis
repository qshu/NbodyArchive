      SUBROUTINE KSLIST(IPAIR)
*
*
*       KS perturber selection.
*       -----------------------
*
      INCLUDE 'common6.h'
      INCLUDE 'kscmn6.h'
      COMMON/SLOW0/  RANGE,ISLOW(10)
*
*
*       Set component & c.m. index and form semi-major axis & eccentricity.
      I1 = 2*IPAIR - 1
      I = N + IPAIR
      SEMI = -0.5D0*BODY(I)/H(IPAIR)
      EB = -0.5*BODY(I1)*BODY(I1+1)/SEMI
      ECC2 = (1.0 - R(IPAIR)/SEMI)**2 + TDOT2(IPAIR)**2/(BODY(I)*SEMI)
      ECC = SQRT(ECC2)
*
*       Use semi-major axis and/or RMIN for perturber selection.
      IF (EB.LT.EBH) THEN
          RAP = SEMI*(1.0 + ECC)
      ELSE
*       Include a tapered criterion depending on energy for soft binaries.
          IF (EB.LT.0.0) THEN
              ZFAC = 1.0 + ABS(EB - EBH)/ABS(EBH)
          ELSE
              ZFAC = 1.0
          END IF
*       Adopt actual apocentre for perturber selection if R > SEMI.
          IF (SEMI.GT.0.0D0.AND.R(IPAIR).GT.SEMI) THEN
              RAP = SEMI*(1.0 + ECC)
          ELSE
*       Accept separation limit for hyperbolic case.
              RAP = MAX(ZFAC*SEMI,R(IPAIR))
          END IF
*       Ensure extra perturbers at new regularizations (R may be small).
          IF (IPHASE.GT.0.AND.SEMI.GT.0.0) THEN
              RAP = SEMI*(1.0 + ECC)
          END IF
      END IF
*
*       Restrict perturber selection for massive eccentric binary.
      IF (BODY(I).GT.100.0*BODYM.AND.
     &   (R(IPAIR).LT.0.1*SEMI.OR.SEMI.LT.0)) THEN
          RAP = MIN(RMIN,0.1*ABS(SEMI))
      END IF
*
*       Include extra perturbers for short periods.
      IF (SEMI.GT.0.0) THEN
          TK = TWOPI*SEMI*SQRT(SEMI/BODY(I))
          IMOD = KSLOW(IPAIR)
          IF (IMOD.GT.1) THEN
              TK = FLOAT(ISLOW(IMOD))*TK
          END IF
          IF (0.25*TK.LT.STEP(I)) THEN
              RAP = SEMI*(1.0 + ECC)
          END IF
      END IF
*
      RCRIT2 = 2.0*RAP**2/BODY(I)
      RFAC = RCRIT2*RAP/GMIN
*       Base fast search on maximum single perturber mass (BODY1).
      RCRIT2 = RCRIT2*BODY1*CMSEP2
*
*       Select new perturbers from the neighbour list.
      NNB1 = 1
      NNB2 = LIST(1,I) + 1
      DO 10 L = 2,NNB2
          J = LIST(L,I)
          W1 = X(1,J) - X(1,I)
          W2 = X(2,J) - X(2,I)
          W3 = X(3,J) - X(3,I)
          RSEP2 = W1*W1 + W2*W2 + W3*W3
*       Include any merged c.m. or chain c.m. bodies in the fast test.
          IF (RSEP2.LT.RCRIT2.OR.NAME(J).LE.0) THEN
*       Estimate unperturbed distance from tidal limit approximation.
              IF (RSEP2*SQRT(RSEP2).LT.RFAC*MAX(BODY(I),BODY(J))) THEN
                  NNB1 = NNB1 + 1
                  LIST(NNB1,I1) = J
              ELSE IF (J.GT.N) THEN
*       Employ a more generous criterion for possible wide binary.
                  JPAIR = J - N
                  IF (LKSINT(JPAIR)) THEN
                      HJ = HX(JPAIR)
                  ELSE
                      HJ = H(JPAIR)
                  END IF
                  RJ = MIN(10.0*ABS(SEMI),-BODY(J)/HJ)
                  IF (RSEP2.LT.CMSEP2*RJ**2) THEN
                      NNB1 = NNB1 + 1
                      LIST(NNB1,I1) = J
                  END IF
              END IF
          END IF
   10 CONTINUE
*
*       Check case of no perturbers (dual purpose).
      IF (NNB1.EQ.1) THEN
*       Add distant perturber for hyperbolic orbit.
          IF (SEMI.LT.0.0) THEN
              NNB1 = 2
              LIST(2,I1) = LIST(2,I)
              GO TO 20
          END IF
*
*       Restrict look-up time to one period for active PN binary (< 1000*RZ).
          IF (KZ(11).NE.0) THEN
              RP = SEMI*(1.0 - ECC)
              IF (RP.LT.1000.0*RZ) THEN
                  IGR = 1
              ELSE
                  IGR = 0
              END IF
*        Note transition from perturbed to unperturbed PN state in KSINT.
              IF (IGR.GT.0) THEN
                  STEP(I1) = TWOPI*SEMI*SQRT(SEMI/BODY(I))
                  DT = MIN(2.0*STEP(I1),STEP(I))
                  CALL STEPK(DT,DTN)
                  DO WHILE (DMOD(T0(I1),DTN).NE.0.0D0)
                      DTN = 0.5 * DTN
                  END DO
                  STEP(I1) = DTN
                  GO TO 20
              END IF
          END IF
*
          IF (KZ(27).LE.0) THEN
*       Specify one unperturbed period at apocentre (NB! check STEP(I)).
              STEP(I1) = TWOPI*SEMI*SQRT(SEMI/BODY(I))
              STEP(I1) = MIN(STEP(I1),STEP(I))
*       Include quantization (interval shortened a little so compensate).
              DT = 2.0*STEP(I1)
              CALL STEPK(DT,DTN)
              DO WHILE (DMOD(T0(I1),DTN).NE.0.0D0)
                  DTN = 0.5 * DTN
              END DO
              STEP(I1) = DTN
          ELSE
*       Maintain perturbed motion during Chaos events (not ROCHE/SPIRAL).
              IF (KSTAR(I).EQ.-1) THEN
                  IF (LIST(1,I1).GT.0) THEN
                      NNB1 = 2
                      LIST(2,I1) = N
                  END IF
              ELSE
*       Avoid possible small period for standard binary (hence use c.m.).
                  STEP(I1) = STEP(I)
                  DT = 2.0*STEP(I1)
                  CALL STEPK(DT,DTN)
                  DO WHILE (DMOD(T0(I1),DTN).NE.0.0D0)
                      DTN = 0.5 * DTN
                  END DO
                  STEP(I1) = DTN
*       Note reduction in BRAKE4 in case of active PN phase.
              END IF
          END IF
      END IF
*
*       Save perturber membership.
   20 LIST(1,I1) = NNB1 - 1
*
      RETURN
*
      END
