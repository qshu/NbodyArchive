      SUBROUTINE KSRES2(J,J1,J2,RIJ2,IZZ)
*
*
*       Coordinates & velocities of KS pair.
*       ------------------------------------
*
      INCLUDE 'common6.h'
      COMMON/SLOW0/  RANGE,ISLOW(10)
      REAL*8  UI(4),RDOT(3),V(4),A1(3,4)
      LOGICAL LPR
*
*
      DTCHK1=0.D0
      DTCHK2=0.D0
      DTCHK3=0.D0
      LPR = .TRUE.
*     LPR = ABS(TIME-0.234928516D0).LT.1.D-2 .AND.
*    *    IZZ.EQ.147.AND.J.EQ.23
*       Skip merged ghost binary (defined by zero total mass).
      IF (BODY(N+J).LE.0.0D0) GO TO 40
*
*       Resolve components of pair #J at curent time.
      J2 = J + J
      J1 = J2 - 1
      A2 = 1.0/R(J)
      A3 = A2*(TIME - T0(J1))
      IF (KSLOW(J).GT.1) THEN
          IMOD = KSLOW(J)
          A3 = A3/FLOAT(ISLOW(IMOD))
      END IF
*
*       Decide appropriate order for interpolation & prediction.
      IF (RIJ2.GT.625.0*R(J)**2) THEN
*       Convert physical interval to regularized time (second order only).
          DTU = (1.0 - 0.5D0*TDOT2(J)*A2*A3)*A3
*
          DTCHK1 = DTU
          A4 = 3.0D0*TDOT2(J)**2*A2 - TDOT3(J)
          DTCHK3 = ((ONE6*A4*A3 - 0.5D0*TDOT2(J))*A2*A3 + 1.0)*A3
          IF (ABS(DTU).GT.DTAU(J)) THEN
              DTU = DTAU(J)
          END IF
*
*       Predict regularized coordinates of distant pair to second order.
          DO 10 K = 1,4
              UI(K) = (FU(K,J)*DTU + UDOT(K,J))*DTU + U0(K,J)
              V(K) = (3.0*FUDOT(K,J)*DTU + 2.0*FU(K,J))*DTU + UDOT(K,J)
   10     CONTINUE
      ELSE
*        Expand regularized time interval to third order.
          A4 = 3.0D0*TDOT2(J)**2*A2 - TDOT3(J)
          DTU = ((ONE6*A4*A3 - 0.5D0*TDOT2(J))*A2*A3 + 1.0)*A3
          DTCHK2 = DTU
*       Apply safety test near small pericentre or for unperturbed motion.
          IF (DTU.GT.DTAU(J)) THEN
              DTU = 0.8*DTAU(J)
          END IF
          IF (LIST(1,J1).EQ.0) DTU = 0.0
*
          IF (LIST(1,J1).GT.0) THEN
          IF(LPR)THEN
          IF(DTCHK1.GT.1.01D0*DTAU(J))PRINT*,'=======DTCHK1/DTAU===',
     *     DTCHK1/DTAU(J)
          IF(DTCHK2.GT.1.01D0*DTAU(J))PRINT*,'=======DTCHK2/DTAU===',
     *     DTCHK2/DTAU(J)
          IF(DTCHK2.GT.1.01D0*DTAU(J).OR.DTCHK1.GT.1.01D0*DTAU(J))THEN
          A4 = 3.0D0*TDOT2(J)**2*A2 - TDOT3(J)
          DTCHK3 = ((ONE6*A4*A3 - 0.5D0*TDOT2(J))*A2*A3 + 1.0)*A3
          PRINT*,' Term 1: A3=',A3,' A2=',A2,' R=',R(J),TIME-T0(J1)
          PRINT*,' Term 2: ',-0.5D0*TDOT2(J)*A2*A3*A3,' TDOT2=',
     *           TDOT2(J)
          PRINT*,' Term 3: ',ONE6*A4*A3*A2*A3*A3,' ONE6=',ONE6
          PRINT*,IZZ,' Bin ',J,' STEP,T0,TIME,DT=',
     *  STEP(J1),T0(J1),TIME,TIME-T0(J1)
          PRINT*,' DTCHK1,2,3,DTAU(J)=',DTCHK1,DTCHK2,DTCHK3,DTAU(J)
          SEMI = -0.5D0*BODY(N+J)/H(J)
      ECC2 = (1.0-R(J)/SEMI)**2 + TDOT2(J)**2/(BODY(N+J)*SEMI)
      RAP = SEMI*(1.0 + SQRT(ECC2))
      RPE = SEMI*(1.0 - SQRT(ECC2))
          PRINT*,' RPE,R,SEMI,RAP=',RPE,R(J),SEMI,RAP,TDOT2(J)
          PRINT*,' KSLOW,GAMMA,H,E=',KSLOW(J),GAMMA(J),H(J),
     *    DSQRT(ECC2)
          PERD = TWOPI*DSQRT(SEMI**3/BODY(N+J))
          PRINT*,'XXX,PERD=',ETAU/DSQRT(ABS(2.D0*H(J))),PERD
          UUU4 = U(1,J)**2 + U(2,J)**2 + U(3,J)**2 + U(4,J)**2
          PRINT*,' U^2=',UUU4,' (r-u^2)/r=',(R(J)-UUU4)/R(J)
          PRINT*,'--------------------------------------'
          CALL FLUSH(6)
          END IF
          END IF
          END IF
*       Predict regularized coordinates to third order.
          DTU1 = 0.0416666666666667*DTU
          DTU2 = ONE6*DTU
          DO 20 K = 1,4
              UI(K) = (((FUDOT2(K,J)*DTU1 + FUDOT(K,J))*DTU +
     &                           FU(K,J))*DTU + UDOT(K,J))*DTU + U0(K,J)
              V(K) = ((FUDOT2(K,J)*DTU2 + 3.0D0*FUDOT(K,J))*DTU +
     &                                    2.0D0*FU(K,J))*DTU + UDOT(K,J)
   20     CONTINUE
      END IF
*
*       Employ KS transformation.
      Q1 = UI(1)**2 - UI(2)**2 - UI(3)**2 + UI(4)**2
      Q2 = UI(1)*UI(2) - UI(3)*UI(4)
      Q3 = UI(1)*UI(3) + UI(2)*UI(4)
      Q2 = Q2 + Q2
      Q3 = Q3 + Q3
      J3 = N + J
      A2 = BODY(J2)/BODY(J3)
*
*       Set global coordinates of regularized components.
      X(1,J1) = X(1,J3) + A2*Q1
      X(2,J1) = X(2,J3) + A2*Q2
      X(3,J1) = X(3,J3) + A2*Q3
      X(1,J2) = X(1,J1) - Q1
      X(2,J2) = X(2,J1) - Q2
      X(3,J2) = X(3,J1) - Q3
*
*       Set current transformation matrix and two-body separation.
      CALL MATRIX(UI,A1)
      RI = UI(1)**2 + UI(2)**2 + UI(3)**2 + UI(4)**2
      RINV = 2.0/RI
*
*       Obtain relative velocities from KS transformation.
      DO 30 L = 1,3
          RDOT(L) = 0.0D0
          DO 25 K = 1,4
              RDOT(L) = RDOT(L) + A1(L,K)*V(K)*RINV
   25     CONTINUE
   30 CONTINUE
*
*       Set global velocities of KS components.
      XDOT(1,J1) = XDOT(1,J3) + A2*RDOT(1)
      XDOT(2,J1) = XDOT(2,J3) + A2*RDOT(2)
      XDOT(3,J1) = XDOT(3,J3) + A2*RDOT(3)
      XDOT(1,J2) = XDOT(1,J1) - RDOT(1)
      XDOT(2,J2) = XDOT(2,J1) - RDOT(2)
      XDOT(3,J2) = XDOT(3,J1) - RDOT(3)
*
   40 RETURN
*
      END
